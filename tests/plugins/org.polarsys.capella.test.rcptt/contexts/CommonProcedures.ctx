--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: CommonProcedures
Element-Type: context
Element-Version: 2.0
Id: _0keAwAMFEeaSHsy4D5kUow
Runtime-Version: 2.0.0.201506042314
Save-Time: 12/12/16 10:20 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

// Iterate through all tab folders and find the one with the active tab given as input
proc "find-tab-folder" [val activePage] {
  loop [val index [int 0]] {
    let [val folder [get-tab-folder -index $index]] {
      let [val folderActivePage [$folder | get-property activePage -raw]] {
        if [$folderActivePage | eq $activePage] {
          $folder //returning result
        } -else { recur [$index | plus 1] }
      }
    }
  }
}

proc "create-project" [val projectName] {
	get-menu "File/New/Capella Project" | click
	with [get-window "New Capella Project"] {
	    get-editbox | set-text $projectName
	    get-button Finish | click
	}
	
	// Check the project is created with the correct sub elements
	with [get-view "Project Explorer" | get-tree] {
	    get-item $projectName | get-property caption | equals $projectName | verify-true
	    get-item $projectName | get-property "getItems().TreeItem[0].getText()" | equals [concat $projectName ".afm"] | verify-true
	    get-item $projectName | get-property "getItems().TreeItem[1].getText()" | equals [concat $projectName ".aird"] | verify-true
	    get-item $projectName | get-property "getItems().TreeItem[2].getText()" | equals [concat $projectName ".capella"] | verify-true
	}
}

proc "clean-workspace-before-restart" {
	delete-file [uri-from-path [concat [get-workspace-location] "/.metadata/.plugins/org.eclipse.core.runtime"]]
}
	
proc "prepare-AUT-after-restart" {
	// Remove the Welcome screen
	get-menu "Help/Welcome" | click
	get-view Welcome | close
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
